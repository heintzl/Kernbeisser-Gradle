

plugins {
    id 'java'
    id 'idea'
    id "io.freefair.lombok" version "5.3.0"
    id 'com.diffplug.gradle.spotless' version '4.0.1'
    id 'com.github.jakemarsden.git-hooks' version '0.0.2'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'Kernbeisser'
version '2.0'

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    maven {
        url "https://jaspersoft.jfrog.io/jaspersoft/third-party-ce-artifacts/"
    }
}

gitHooks {
    hooks = ['pre-commit': 'spotlessApply']
}

test {
    useJUnit()
}

dependencies {
    constraints {
        implementation("org.apache.logging.log4j:log4j-core") {
            version {
                strictly("[2.17.1, 3[")
                prefer("2.17.1")
            }
            because("CVE-2021-44228: Log4j vulnerable to remote code execution")
        }
    }
    def hibernateVersion = '5.3.12.Final'
    implementation 'org.hibernate:hibernate-agroal:'+hibernateVersion
    implementation "org.hibernate:hibernate-envers:"+hibernateVersion
    implementation files("${rootDir}/libs/forms_rt.jar")
    implementation 'org.mariadb.jdbc:mariadb-java-client:2.7.2'
    implementation 'at.favre.lib:bcrypt:0.9.0'
    implementation "com.github.jiconfont:jiconfont-font_awesome:4.7.0.1"
    implementation "com.github.jiconfont:jiconfont-swing:1.0.0"
    implementation 'org.apache.logging.log4j:log4j-api:2.17.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.17.1'
    implementation 'net.sf.jasperreports:jasperreports:6.15.0'
    implementation 'net.sf.jasperreports:jasperreports-fonts:6.15.0'
    implementation 'org.jfree:jfreechart:1.5.0'
    implementation 'com.formdev:flatlaf:2.4'
    implementation 'org.json:json:20190722'
    implementation 'org.jetbrains:annotations:15.0'
    implementation 'com.google.code.gson:gson:2.8.6'


    implementation 'com.google.guava:guava:30.0-jre'
    implementation 'com.opencsv:opencsv:5.3'
    implementation group: 'org.ow2.asm', name: 'asm', version: '7.0'
    implementation 'org.javassist:javassist:3.27.0-GA'

    implementation group: 'com.github.lgooddatepicker', name: 'LGoodDatePicker', version: '11.2.1'


    testImplementation 'org.mockito:mockito-inline:3.7.7'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.7.7'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
}

jar {
    manifest {
        attributes 'Main-Class': 'kernbeisser.Main'
    }
 //   fileMode 750
}

shadowJar {
    append 'jasperreports_extension.properties'
}

task copyAgent(type: Copy){
    from 'src/main/resources/Agent/Agent-1.0-SNAPSHOT.jar'
    into "$buildDir/libs"
 //   fileMode 750
}

task copyProductionFolderContent(type: Copy){
    from 'production'
    into "$buildDir/libs"
//    fileMode 750
}

task copyReportsFolder(type: Copy){
    from 'reports'
    into "$buildDir/libs/reports"
//    fileMode 750
}

//task chmodRecursively(type: Exec){
//    commandLine "chmod", "-R", "755", "$buildDir"
//}

copyProductionFolderContent.dependsOn copyAgent
processResources.dependsOn copyProductionFolderContent, copyReportsFolder//, chmodRecursively

spotless {
    java {
        googleJavaFormat()
    }
}