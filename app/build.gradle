plugins {
    id 'java'
    id 'idea'
    id 'com.diffplug.spotless' version "6.25.0"
    id 'com.github.jakemarsden.git-hooks' version '0.0.2'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id("io.freefair.lombok") version "8.6"
}
spotless {
    java {
        toggleOffOn()
    }
}

group 'Kernbeisser'
version '2.0'
compileJava.options.encoding = 'UTF-8'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://jaspersoft.jfrog.io/jaspersoft/third-party-ce-artifacts/"
    }
}

gitHooks {
    hooks = ['pre-commit': 'spotlessApply']
}


test {
    useJUnit()
}

dependencies {
    constraints {
        implementation("org.apache.logging.log4j:log4j-core") {
            version {
                strictly("[2.17.1, 3[")
                prefer("2.17.1")
            }
            because("CVE-2021-44228: Log4j vulnerable to remote code execution")
        }
    }
    def hibernateVersion = '6.4.4.Final'
    implementation 'org.hibernate:hibernate-core:'+hibernateVersion
    implementation "org.hibernate:hibernate-envers:"+hibernateVersion
    implementation files("libs/forms_rt.jar")
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.3.3'
    implementation 'at.favre.lib:bcrypt:0.9.0'
    implementation "com.github.jiconfont:jiconfont-font_awesome:4.7.0.1"
    implementation 'com.github.jiconfont:jiconfont-swing:1.0.1'
    implementation 'org.apache.logging.log4j:log4j-api:2.17.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.17.1'
    implementation 'net.sf.jasperreports:jasperreports:7.0.0'
    implementation 'net.sf.jasperreports:jasperreports-fonts:7.0.0'
    implementation 'net.sf.jasperreports:jasperreports-pdf:7.0.0'
    implementation 'org.jfree:jfreechart:1.5.3'
    implementation 'com.formdev:flatlaf:2.4'
    implementation 'org.json:json:20220320'
    implementation 'org.jetbrains:annotations:23.0.0'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'com.opencsv:opencsv:5.6'
    implementation group: 'com.github.lgooddatepicker', name: 'LGoodDatePicker', version: '11.2.1'
    implementation 'org.slf4j:slf4j-nop:2.0.13'

    //Bytecode Tools
    implementation group: 'org.ow2.asm', name: 'asm', version: '9.6'
    implementation 'org.javassist:javassist:3.27.0-GA'

    //Unsafe alternative
    implementation 'org.objenesis:objenesis:1.2'

    implementation project(":security")
    implementation project(":kbmetamodel")
    implementation 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    annotationProcessor files(project(":kbmetamodel").file("build/libs/kbmetamodel.jar"))

    //Tests
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.11.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.2'
    testImplementation 'org.assertj:assertj-core:3.25.3'
}

jar {
    manifest {
        attributes 'Main-Class': 'kernbeisser.Main'
    }
    archiveFileName = "kernbeisser.jar"
    //   fileMode 750
}

shadowJar {
    append 'jasperreports_extension.properties'
    archiveFileName = "kernbeisser.jar"
}

task copyAgent(type: Copy){
    from '../security/build/libs/security.jar'
    into getLayout().getBuildDirectory().dir("libs")
 //   fileMode 750
}

task copyProductionFolderContent(type: Copy){
    from '../production'
    into getLayout().getBuildDirectory().dir("libs")
//    fileMode 750
}

task copyReportsFolder(type: Copy){
    from '../reports'
    into getLayout().getBuildDirectory().dir("libs/reports")
//    fileMode 750
}
copyAgent.dependsOn project(":security").getTasksByName("jar",false)
copyProductionFolderContent.dependsOn copyAgent
processResources.dependsOn copyProductionFolderContent, copyReportsFolder//, chmodRecursively

spotless {
    java {
        googleJavaFormat()
    }
}